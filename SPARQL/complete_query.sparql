PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX ordo: <http://www.orpha.net/ORDO/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?patient_id ?value_startdate ?record_label ?value ?attribute_type
				#?output_identifier ?output_id
				#?target_type ?specific_method_type 
				#?event_id ?activity_type ?substance ?concentration_value ?frequency_value

WHERE {
  GRAPH ?record {
        			### Core ###
    ?person sio:SIO_000228 ?role .
    ?id sio:SIO_000020 ?role ; sio:SIO_000300 ?patient_id .
    ?role a sio:SIO_000016 ; sio:SIO_000356 ?process .
    
    ?process a ?process_type ;
             sio:SIO_000229 ?output ;
             sio:SIO_000291 ?target ;
             sio:SIO_000230 ?input ;
             sio:SIO_000339 ?protocol ;
             sio:SIO_000028 ?specific_method .
    
    FILTER(?process_type != sio:SIO_000006)
    OPTIONAL { ?process rdfs:comments ?comments }
        
        		### Participants ###
        
    ?output a sio:SIO_000015 ;
        sio:SIO_000628 ?attribute;
        sio:SIO_000671 ?output_identifier.
        
    OPTIONAL { ?output a ?outpute_type . FILTER(?outpute_type != sio:SIO_000015) }
    OPTIONAL { ?output sio:SIO_000300 ?value }
   
    ?attribute a sio:SIO_000614 .
    OPTIONAL { ?attribute a ?attribute_type . FILTER(?attribute_type != sio:SIO_000614) }

    ?output_identifier a sio:SIO_000115.
    OPTIONAL{?output_identifier sio:SIO_000300 ?output_id}

    OPTIONAL { ?input a ?input_type . FILTER(?input_type != sio:SIO_000015) } 
    OPTIONAL { ?target a ?target_type . FILTER(?target_type != sio:SIO_000015) }
    OPTIONAL { ?specific_method a ?specific_method_type . FILTER(?specific_method_type != sio:SIO_000006)}
    
    			### Protocol /Specification ###
    ?protocol a sio:SIO_000090 ;
              sio:SIO_000028 ?frequency, ?substance, ?concentration .
    
    ?substance a sio:SIO_000315 .
    OPTIONAL { ?substance a ?substance_type . FILTER(?substance_type != sio:SIO_000315) }
    
    ?concentration a sio:SIO_001088 .
    OPTIONAL { ?concentration a ?concentration_type . FILTER(?concentration_type != sio:SIO_001088) }
    
    ?frequency a sio:SIO_001367 .
    OPTIONAL { ?frequency a ?frequency_type . FILTER(?frequency_type != sio:SIO_001367) }
    OPTIONAL { ?frequency sio:SIO_000300 ?frequency_value }
  }

  				### Medical history ###
  ?record a sio:SIO_001027 ;
    	  rdfs:label ?record_label ;
          sio:SIO_000680 ?startdate ;
          sio:SIO_000681 ?enddate ;
          sio:SIO_000687 ?age ;
          sio:SIO_000300 ?uniqid ;
          sio:SIO_000068 ?medical_history ;
          sio:SIO_000068 ?event .
  
  ?medical_history a sio:SIO_010673 ; sio:SIO_000332 ?person_timeline .
  ?person_timeline a sio:SIO_000498 ; sio:SIO_000671 ?person_identifier .
  ?person_identifier a sio:SIO_000115 ; sio:SIO_000300 ?pid .

  OPTIONAL { ?age sio:SIO_000300 ?age_value }
  OPTIONAL { ?startdate sio:SIO_000300 ?value_startdate }
  OPTIONAL { ?enddate sio:SIO_000300 ?value_enddate }

  ?age a sio:SIO_001013 .
  ?startdate a sio:SIO_000031.
  ?enddate a sio:SIO_000032.

  					### Event ###
  OPTIONAL {?event a sio:SIO_000088 ; sio:SIO_000300 ?event_id .}
}
ORDER BY ?pid